plugins {
	id 'net.neoforged.moddev'
	id 'java-library'
	id 'maven-publish'
	id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
	distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

base {
	archivesName = "${mod_id}-neoforge-${minecraft_version}"
}

// Mojang ships Java 21 to end users in 1.21.1, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
	version = project.neo_version

	parchment {
		mappingsVersion = project.parchment_mappings_version
		minecraftVersion = project.parchment_minecraft_version
	}

	// This line is optional. Access Transformers are automatically detected
	// accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')

	runs {
		client {
			client()
			gameDirectory = project.file('runs/client')
			jvmArguments = ["-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition"]

			systemProperty("mixin.debug.export", "true")
			systemProperty("mixin.debug.verbose", "true")

			systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
		}

		server {
			server()
			gameDirectory = project.file('runs/server')
			jvmArguments = ["-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition"]

			systemProperty("mixin.debug.export", "true")
			systemProperty("mixin.debug.verbose", "true")

			programArgument '--nogui'
			systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
		}

		// This run config launches GameTestServer and runs all registered game tests, then exits.
		// By default, the server will crash when no game tests are provided.
		// The game test system is also enabled by default for other run configs under the /test command.
		gameTestServer {
			type = "gameTestServer"
			gameDirectory = project.file('runs/game-test')
			systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
		}

		data {
			data()
			gameDirectory = project.file('runs/data')

			// Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
			programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
		}

		// applies to all the run configs above
		configureEach {
//			systemProperty 'forge.logging.markers', 'REGISTRIES'
			systemProperty('terminal.jline', 'true') // enable interactive & colored terminal features

			// https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
			logLevel = org.slf4j.event.Level.DEBUG //doesn't work for some reason

//			systemProperty("forge.logging.console.level", "debug") //force debug level with system property
		}
	}

	mods {
		// define mod <-> source bindings
		// these are used to tell the game which sources are for which mod
		// mostly optional in a single mod project
		// but multi mod projects should define one per mod
		"${mod_id}" {
			sourceSet(sourceSets.main)
		}
	}
}

// Include resources generated by data generators.
sourceSets.main {
	resources {
		srcDir('src/generated/resources')
		exclude(".cache/")
	}
}

// Sets up a dependency configuration called 'localRuntime' and a deobfuscating one called 'modLocalRuntime'
// These configurations should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
	runtimeClasspath.extendsFrom localRuntime
}

dependencies {
//	compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
//	implementation(jarJar("io.github.llamalad7:mixinextras-forge") {
//		version {
//			strictly mixin_extras_version_range
//			prefer mixin_extras_version
//		}
//	})
//
//	annotationProcessor('org.spongepowered:mixin:0.8.5:processor')

	api("org.jspecify:jspecify:1.0.0")
	api('org.apache.commons:commons-numbers-combinatorics:1.2')
	additionalRuntimeClasspath('org.apache.commons:commons-numbers-combinatorics:1.2') // we only need to define the additional runtime classpath for Minecraft versions <= 1.21.8

	localRuntime("curse.maven:connectivity-470193:7032388") // fixes issues with big network packets due to 50k items/recipes
	localRuntime("curse.maven:cupboard-326652:6078150") // dependency of Connectivity mod

	localRuntime("curse.maven:modernfix-790626:7200212")
}

jar {
	exclude('data')
	exclude('canterbury_corpus')
	exclude('com/github/elenterius/orb/dev')
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
	var replaceProperties = [
			minecraft_version      : minecraft_version,
			minecraft_version_range: minecraft_version_range,
			neo_version: neo_version,
			loader_version_range   : loader_version_range,
			mod_id                 : mod_id,
			mod_name               : mod_name,
			mod_license            : mod_license,
			mod_version            : mod_version,
			mod_authors            : mod_authors,
			mod_description: mod_description,
			mod_url        : mod_url,
			mod_issue_url  : mod_issue_url
	]
	inputs.properties replaceProperties
	expand replaceProperties
	from "src/main/templates"
	into "build/generated/sources/modMetadata"
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
	publications {
		register('mavenJava', MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "file://${project.projectDir}/repo"
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

idea {
	module {
		downloadSources = true
		downloadJavadoc = true
	}
}
